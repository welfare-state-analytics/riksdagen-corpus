SELECT DISTINCT ?wiki_id ?governmentLabel ?roleLabel ?start ?end
WHERE {
  {
  VALUES ?government { wd:Q3046743 wd:Q4570153 wd:Q4570314 wd:Q4570330 wd:Q4570354 wd:Q4570378 wd:Q4570967 wd:Q4907240 wd:Q4907653 wd:Q4957676 wd:Q5217837 wd:Q5306179 wd:Q5306186 wd:Q5306190 wd:Q5306200 wd:Q10650416 wd:Q10650420 wd:Q10650424 wd:Q10650438 wd:Q10650456 wd:Q18176361 wd:Q60736270 wd:Q107443672 wd:Q109705067 } 
  ?government p:P527 ?pMinister.
  ?pMinister  ps:P527 ?wiki_id;
              pq:P3831 ?role
  OPTIONAL{?pMinister pq:P580 ?start}
  OPTIONAL{?pMinister pq:P582 ?end}
  } UNION {

  ?wiki_id wdt:P31 wd:Q5;
           p:P39 ?posheld.

  # Position held in government
  ?government wdt:P31 wd:Q930729.
  ?posheld pq:P5054 ?government; # Position held in a government
           ps:P39 ?role.
  OPTIONAL { ?posheld pq:P580 ?start. }
  OPTIONAL { ?posheld pq:P582 ?end. }
  }
    
  SERVICE wikibase:label { bd:serviceParam wikibase:language "sv". }
#  ?MinisterQ rdfs:label ?minister. Filter(lang(?minister)="sv")
}