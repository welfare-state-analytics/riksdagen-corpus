SELECT DISTINCT ?wiki_id ?wiki_idLabel
WHERE
{
    ?wiki_id p:P39 ?posheld. # With position held
  
    {
    # Position held is in one of the chambers
    VALUES ?chamber { wd:Q10655178 wd:Q33071890 wd:Q81531912 }
    ?posheld ps:P39 ?chamber. # Position held in a chamber
    } UNION {
    VALUES ?role { wd:Q1850749 wd:Q85679583 wd:Q85679895 wd:Q110785766 wd:Q110785785 wd:Q110811796 wd:Q110811796 wd:Q110811805 wd:Q110812751 wd:Q110812759 }
    ?posheld ps:P39 ?role. # Position held as speaker
    } UNION {
    VALUES ?role { wd:Q687075 wd:Q105359146 }
    ?posheld ps:P39 ?role. # Position held as (vice) prime minister
    } UNION {
    ?government wdt:P31 wd:Q930729.
    ?posheld pq:P5054 ?government. # Position held in government
    }
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "sv". }
}